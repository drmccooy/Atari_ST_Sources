	.LIST
	.FILE
;*******************************************************************
; FEHLERFREIES PROGRAMM FUER VERSUCH 2 (DEBUGGING)
;*******************************************************************

CR	=$D	; WAGENRUECKLAUF
LF	=$A	; ZEILENTRANSPORT

TEROUT	=$F009	; SENDE ASCII-ZEICHEN ZUM TERMINAL AUS AKKU A
PRINT	=$F00F	; AUSGABE EINES ASCII STRINGS INDIZIERT DURCH X
INPHEX	=$F018	; EINLESEN EINES HEX-ZEICENS IN AKKU A


	.ORG	$2000		; STARTADRESSE DES PROGRAMMS

START	LDS	#$3000		; USER-STACK LIEGT AUF ADRESSE $3000
	LDX	#HALLO
	JSR	PRINT		; PROGRAMMANFANG MELDEN
LOOP	LDX	#HEXTXT
	JSR	PRINT		; EINGABE EINER HEX-ZAHL ANFORDERN
	JSR	INPHEX		; HEX-ZAHL IN AKKU A EINLESEN
	PSHA			; EINGABE IM STACK RETTEN
	LDX	#DEZTXT
	JSR	PRINT		; "DEZIMALWERT=" AUSGEBEN
	PULB			; HEX-WERT WIEDER LADEN
	LDX	#DEZAHL+3	; ADRESSE VOM ENDE DES DEZIMALSTRINGS 
	CLR	,X		; ENDE MIT ASCII-NUL MARKIEREN
UMWAND	DEX			; STRINGZEIGER UM EINE STELLE VERMINDERN
	JSR	DIVA10		; EINGABE DURCH 10 TEILEN
	ADDA	#'0		; DIVISIONSREST IN ASCII-ZIFFER UMWANDELN
	STAA	,X		;    UND IN STRING ABSPEICHERN
	CMPB	#9		; QUOTIENT > 9 ?
	BHI	UMWAND		; JA: DANN WEITER UMWANDELN...
	TSTB			; QUOTIENT = 0 ?
	BEQ	DEZOUT		; JA: DANN KEINE FUEHRENDE NULL ERZEUGEN
	ADDB	#'0		; QUOTIENT IN ASCII-ZIFFER UMWANDELN
	DEX			; STRINGZEIGER UM EINE STELLE VERMINDERN
	STAB	,X		;    UND ZIFFER IN STRING SPEICHERN
DEZOUT	JSR	PRINT		; UMGEWANDELTE ZAHL AUSGEBEN
	BRA	LOOP		; UND NEUE HEX-ZAHL ANFORDERN


; DIVISION VON AKKU-B DURCH 10, QUOTIENT NACH B, REST NACH A

DIVA10	PSHX			; MOMENTANE STRINGPOSITION RETTEN
	LDX	#8		; SCHIEBEDIVISION FUER 8 BIT
	CLRA			; HIGHER BYTE VOM D-AKKU LOESCHEN
DIV	ASLD			; ZAEHLER 1 BIT NACH LINKS SCHIEBEN
	CMPA	#10		; 10 ENTHALTEN ?
	BCS	NOSUB		; NEIN...
	SUBA	#10		; JA: DANN ABZIEHEN
	INCB			;    UND QUOTIENTENBIT SETZEN
NOSUB	DEX			; ALLE 8 BITS BEARBEITET ?
	BNE	DIV		; NEIN...
	PULX			; ALTE STRINGPOSITION WIEDER LADEN
	RTS


; TEXTE ZUR KOMMUNUKATION MIT DEM USER
	
	.ORG	$2200		; TEXTE FANGEN AB ADRESSE $2200 AN
HALLO	.BYTE	CR,LF,LF
	.ASCIZ	"Umwandlung HEX -> DEZ"

HEXTXT	.BYTE	CR,LF
	.ASCIZ	"HEX-Zahl=$ "

DEZTXT	.ASCIZ	"  Dezimalzahl= "

DEZAHL	.BLKB	4		; 4 BYTES RESERVIEREN

	.END

