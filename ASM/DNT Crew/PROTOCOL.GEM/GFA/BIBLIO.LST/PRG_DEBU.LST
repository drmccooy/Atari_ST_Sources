> PROCEDURE on_tube_prg
  '
  ' Proc‚dure devant ˆtre ex‚cut‚e … l'initialisation d'un PRG
  ' afin que celui-ci se d‚clare comme compatible protocole
  ' Fichier PRG_DEBU.LST
  '
  LOCAL maxi&,i&,temp%,sortie!
  jy_suis!=TRUE         ! suis-je dans la liste ? par d‚faut, oui...
  cookie1%=CVL("_TUB")  ! Cookie recherch‚
  cookie2%=0            ! pas de cookie-jar … mettre
  cookie3%=0            ! nous ne pouvons pas mettre de tableau (car PRG!!)
  cookie4&=0            ! prenons juste l'adresse...
  @cookie_jar
  ' S'il y a un tableau avec une adresse de liste protocole...
  IF cookie2%<>0 AND cookie3%<>0
    ' Nous v‚rifions l'en-tˆte de cette liste protocole (PROT)
    ' et si c'est correct, nous rajoutons notre APP_ID dans la liste
    IF LPEEK(cookie3%)=CVL("PROT")    ! si c'est = PROT
      maxi&=DPEEK(cookie3%+4)   ! nbr d'app_id autoris‚es
      cookie3%=cookie3%+6     ! saute les 6 octets d'en-tˆte
      ' On recherche le flag de fin de la liste (&HFFFF)
      ' Il y a 3 choses … tester:
      ' est-ce qu'il y a d‚ja mon APP_ID (cas d'un PRG ayant
      ' quitt‚ en plantant par exemple)
      ' est-ce que l'on trouve le flag de fin
      ' est-ce que l'on ne d‚passe le nombre d'entr‚es maxi. de la liste
      '
      sortie!=FALSE
      REPEAT
        EXIT IF DPEEK(cookie3%)=app_id&   !si c'est mon app_id,bye bye...
        IF DPEEK(cookie3%)=&HFFFF   ! flag de fin ?
          DPOKE cookie3%,app_id&    ! donc je place mon APP_ID
          DPOKE cookie3%+2,&HFFFF   ! et je remet le flag de fin
          jy_suis!=TRUE             ! j'y suis
          sortie!=TRUE              ! pour forcer la sortie
        ELSE
          IF maxi&=0                    ! plus de place...
            ~FORM_ALERT(1,alerte_tub2$) ! indique plus de place
            jy_suis!=FALSE              ! donc je n'y suis pas...
            sortie!=TRUE                ! bye bye...
          ENDIF
        ENDIF
        DEC maxi&               ! compte un APP_ID
        cookie3%=cookie3%+2     ! passe 2 octets (chaque app_id est sur 2 oct)
      UNTIL sortie!=TRUE
      '
    ELSE
      ~FORM_ALERT(1,alerte_tub1$)       ! mauvaise liste
    ENDIF
  ENDIF
RETURN
