		OPT O+

; *******************************
; *                             *
; *        Multi Square         * 
; *                             *
; *    BORDER SCROLL ROUTINE    *
; *                             *
; *        AUTOMATION 89        *
; *                             *
; *  BY THE LAW: 24th DEC / 89  *
; *                             *
; *******************************



;
; RESERVE MEMORY FOR GEMDOS
;

	MOVE.L 4(A7),A5
	MOVE.L #$100,-(SP)
	MOVE.L A5,-(SP)
	MOVE.L #$4A0000,-(SP)
	TRAP #1
	ADD.W #12,SP

; CALL THE CODE

	BSR FIRST

	PEA CALL_ALL
	MOVE.W #$26,-(SP)
	TRAP #14
	ADD.W #6,SP

	BSR SECND

	PEA VAMP
	MOVE.W #$26,-(SP)
	TRAP #14
	ADD.W #6,SP

; WHICH TERMINATE ROUTINE?

	MOVE.W #0,-(SP)
	MOVE.L OLD,-(SP)
	MOVE.L OLD,-(SP)
	MOVE.W #5,-(SP)
	TRAP #14
	ADD.W #12,SP

	BSR MED_RES

	CMP.L #'TERM',RD4
	BNE LOADIT

	MOVE.W #$4C,-(SP)
	TRAP #1

LOADIT	CLR.L -(SP)
	MOVE.L #CT,-(SP)
	MOVE.L RD4,-(SP)
	CLR.W -(SP)
	MOVE.W #$4B,-(SP)
	TRAP #1

	MOVE.W #$4C,-(SP)
	TRAP #1

MED_RES	CMP.L #'MED!',RESO
	BNE RET
	PEA SETREZ
	MOVE.W #9,-(SP)
	TRAP #1
	ADD.W #6,SP
RET	RTS


RD4	DC.L 0

*****************************


;
; ALL CODE HERE!!
;


CALL_ALL
	MOVE.W #2,-(SP)
	TRAP #14
	ADD.W #2,SP
	MOVE.L D0,OLD

	LEA CHARSET+2,A0
	LEA PAL2,A1
	MOVEQ.W #15,D0
POI	MOVE.W (A0)+,(A1)+
	DBRA D0,POI

	LEA $FF8240,A0
	LEA COLBUFF,A1
	MOVEQ.W #15,D0
SCOLS	MOVE.W (A0)+,(A1)+
	DBRA D0,SCOLS

	MOVE.B $484,KB

	CLR.W -(SP)
	PEA $40000
	PEA $40000
	MOVE.W #5,-(SP)
	TRAP #14
	ADD.W #12,SP

;
; DISABLE MOUSE
;

	MOVE.B #$12,$FFFC02

;
; SET SCROLLTEXT POINTERS
;

	MOVE.L #TEXT,TPNT
	MOVE.L #CHARSET+34,STSTART

;
; SET VERTICAL INTERUPT ON
;

	MOVE.L #$40000+(160*204)-2,SCRLADD


	MOVE.L $70,STORE
	MOVE.L #VBL,$70


;
; SET NEW KEYBOARD INTERUPT 
;

	MOVE.L $118,S118
	MOVE.L #NEWKEY,$118

;
; SET MUSIC AND DRAW PICTURES
;

	MOVE.W #2,D0
	LEA MUSIC,A0
	JSR (A0)

	LEA PIC+2,A0
	LEA $FF8240,A1
	MOVEQ.W #15,D0
CO	MOVE.W (A0)+,(A1)+
	DBRA D0,CO

	BSR DRAWPIC

;
; SET HORIZ-BLANK
;

	PEA HBL
	MOVE.W #1,-(SP)
	MOVE.W #%1000,-(SP)
	MOVE.W #1,-(SP)
	MOVE.W #31,-(SP)
	TRAP #14
	ADD.W #12,SP

	LEA $48000-(160*5),A0
	MOVE.W #10,D0
ERASE	CLR.L (A0)+
	DBRA D0,ERASE


;
; MEMORY ADDRESS OF SCROLL
;


	MOVE.W #5,-(SP)
	MOVE.W #26,-(SP)
	TRAP #14
 	ADD.W #4,SP

	
;
; WAIT FOR HOT-KEYS LOOP
;

WAIT
	MOVE.W TIMER,D0
HANGIT	CMP.W TIMER,D0
	BEQ.S HANGIT

;
; CALL MUSIC
;

	LEA MUSIC,A0
	JSR $E(A0)
	BSR SCROLL


;
; CHECK FOR KEYS
;

	MOVE.B KEY,D0

; CHECK FOR '0' - 50/60 Hz

	CMP.B #$B,D0
	BEQ FLIPFREQ

TEST1	CMP.B #02,KEY
	BNE.S TEST2
	MOVE.L #CHAS,RD4
	BRA.S EXITA
TEST2	CMP.B #03,KEY
	BNE.S TEST3
	MOVE.L #'TERM',RD4
	BRA.S EXITA
TEST3	CMP.B #04,KEY
	BNE.S WAIT
	MOVE.L #'TERM',RD4
	BRA.S EXITA
CHAS	DC.B "CHASE_HQ.LAW",0
	EVEN

EXITA	
	MOVE.W #8,-(SP)
	MOVE.W #26,-(SP)
	TRAP #14
	ADD.W #4,SP

	MOVE.L S118,$118

	MOVE.L STORE,$70

	MOVE.W #5,-(SP)
	MOVE.W #27,-(SP)
	TRAP #14
	ADD.W #4,SP

;
; SWITCH OFF SOUND-CHIP
;

	BSR NOFX

;
; RESTORE KEYBOARD
;

	MOVE.B #$80,$FFFC02
	MOVE.W #$FFFF,D0
UYT	NOP
	DBRA D0,UYT
	MOVE.B #$1,$FFFC02

;
; RESTORE KEY-CLICK
;

	MOVE.B KB,$484

;
; SET FREQUENCY
;

	BSR SETFREQ

; BACK TO THE BEGINING

	RTS

; RESTORE COLOURS

VAMP	LEA $FF8240,A1
	LEA COLBUFF,A0
	MOVEQ.W #15,D0
VAMP2	MOVE.W (A0)+,(A1)+
	DBRA D0,VAMP2
	RTS


DELAY	CLR.W TIMER
	RTS

VBL	MOVEM.L D0-D7/A0-A6,REG2

	CLR.W LINE

	LEA PIC+2,A0
	LEA $FF8240,A1
	MOVEQ.W #15,D0
RE	MOVE.W (A0)+,(A1)+
	DBRA D0,RE

	ADDQ.W #1,HERTZWAIT
	CMP.W #25,HERTZWAIT
	BNE EXITFLA
	MOVE.W #24,HERTZWAIT

EXITFLA

	ADDQ.W #1,TIMER

	MOVEM.L REG2,D0-D7/A0-A6

	RTE

NEWKEY	MOVE.B $FFFC02,KEY
	BCLR.B #6,$FFFFFA11
	RTE

NOFX	MOVE.L #$8080000,$ffff8800.W
	MOVE.L #$9090000,$ffff8800.W
	MOVE.L #$A0A0000,$ffff8800.W
	RTS

BACKO	MOVE.L PAL2,$FF8240
	MOVE.L PAL2+4,$FF8244
	MOVE.L PAL2+8,$FF8248
	CLR.L $FF825C
	MOVE.W #-1,NOW
	BRA HBL2

HBL	TST.W NOW
	BPL.S BACKO

HBL2	MOVE.L D0,-(SP)
	MOVE.B #%11111110,$FF820A
	ADD.W #1,LINE
_199	CMP.W #199,LINE
	BNE _200
	MOVE.B #%11111100,$FF820A
	BRA HBL_RET
_200	CMP.W #201,LINE
	BNE HBL_RET
	MOVE.W #1,NOW
HBL_RET	MOVE.L (SP)+,D0
	BCLR.B #0,$FFFA0F
	RTE

	

SCROLL	
	MOVE.W #2,D5


BLAM	SUBQ.W #1,SCRL
	TST.W SCRL
	BNE GO_SCRL

GET2	MOVE.W #16,SCRL
	MOVEQ.L #0,D1
	MOVE.L TPNT,A0

	MOVE.B (A0),D1

	ADD.L #1,TPNT

	MOVE.B D1,D3
	SUB.W #$20,D1

	CMP.B #$FF,D3

	BNE NEXT

WRAP	MOVE.L #TEXT,TPNT
	MOVE.W #16,SCRL
	BRA.S BLAM

NEXT	MOVE.W #16,SCRL

NEXT_CH
	
_P	CMP.B #"R",D3
	BLT.S _AST
	ASL #4,D1
	ADD.L #(31*160)*5,D1
	BRA OUT
_AST	CMP.B #"H",D3
	BLT.S _4
	ASL #4,D1
	ADD.L #(31*160)*4,D1
	BRA OUT
_4	CMP.B #">",D3
	BLT.S _GT
	ASL #4,D1
	ADD.L #(31*160)*3,D1
	BRA OUT
_GT	CMP.B #"4",D3
	BLT.S _H
	ASL #4,D1
	ADD.L #(31*160)*2,D1
	BRA OUT
_H	CMP.B #"*",D3
	BLT.S _GO
	ASL #4,D1
	ADD.L #(31*160),D1
	BRA.S OUT
_GO	ASL #4,D1

OUT	MOVE.L #CHARSET+34,A5
	ADD.L D1,A5
	LEA CHBUFF(PC),A4
	MOVEQ.L #15,D7
FILLBUF	MOVE.L (A5)+,(A4)+
	MOVE.L (A5),(A4)+
	ADD.L #156,A5
	DBRA D7,FILLBUF

GO_SCRL	LEA CHBUFF(PC),A0
	MOVE.L SCRLADD,A2
	MOVEQ.L #15,D2
	ADDQ.L #8,A2
ROWS	ADD.L #152,A2
	MOVEQ.W #3,D1
PLANES	LSL.W (A0)+
	ROXL.W $98(A2)
	ROXL.W $90(A2)
	ROXL.W $88(A2)
	ROXL.W $80(A2)
	ROXL.W $78(A2)
	ROXL.W $70(A2)
	ROXL.W $68(A2)
	ROXL.W $60(A2)
	ROXL.W $58(A2)
	ROXL.W $50(A2)
	ROXL.W $48(A2)
	ROXL.W $40(A2)
	ROXL.W $38(A2)
	ROXL.W $30(A2)
	ROXL.W $28(A2)
	ROXL.W $20(A2)
	ROXL.W $18(A2)
	ROXL.W $10(A2)
	ROXL.W $8(A2)
	ROXL.W (A2)+
	DBRA D1,PLANES
	DBRA D2,ROWS
	DBRA D5,BLAM

OUTG	
	RTS


REG2	DS.L 20

FLIPFREQ	
	CMP.W #12,HERTZWAIT
	BLT WAIT
	NEG.W FFLAG
	CLR.W HERTZWAIT	
	EOR.W #$222,PAL2

RETURN	BRA WAIT

SETFREQ	TST.W FFLAG
	BPL _50HZ
	MOVE.B #%11111100,$FF820A
	RTS
_50HZ	MOVE.B #%11111110,$FF820A
	RTS

DRAWPIC	LEA PIC+34,A0
	LEA $40000,A1
	MOVE.L #32000/4,D0
TYU	MOVE.L (A0)+,(A1)+
	DBRA D0,TYU
	RTS


********************
* VARIABLES/LABELS *
********************


PTR		dc.l $40000+(180*160)
TIMER		DC.W 0
LINE		DC.W 0
STORE		DC.L 0
DDD		DC.W 1
SPEED		DC.W 1
SCRL		DC.W 1
TPNT		DC.L 0
CHBUFF		DS.L (4*32)
SCRLADD		DC.L 0
STCHAR		DC.L 0
NOW		DC.W -1
KB		DC.W 0
COLBUFF		DS.W 16
CT		DC.L 0
RESO		DC.L 0
FFLAG		DC.W 1
REG		DS.L 50
SHIFTER		DC.W 0
OLD		DC.L 0
HERTZWAIT	DC.W 15
KEY		DC.W 0
S118		DC.L 0
STSTART		DC.L 0
NOW2		DC.W 0
PAL2		DS.W 16
HREG		DC.L 8

FIRST	LEA SAMPLAY,A0
	ADD.L #28,A0

	MOVE.L #SAMPLE+12,2(A0)
	MOVE.L #END,A3
	MOVE.L #SAMPLE+12,A2
	SUB.L A2,A3
	MOVE.L A3,6(A0)
	MOVE.L #2,10(A0)

	JSR (A0)

	RTS

SECND	LEA SAMPLAY,A0
	ADD.L #28,A0

	MOVE.L #SAMPLE1+12,2(A0)
	MOVE.L #END1,A3
	MOVE.L #SAMPLE1+12,A2
	SUB.L A2,A3
	MOVE.L A3,6(A0)
	MOVE.L #2,10(A0)

	JSR (A0)

	RTS

SAMPLE	INCBIN "\MENU.196\WE_MEET.SAM"
END	EVEN
SAMPLE1	INCBIN "\MENU.196\STRIKES.SAM"
END1	EVEN
SAMPLAY	INCBIN "\MENU.196\BASCODE.EXE"
MUSIC	INCBIN "\MENU.196\VERMIN"
	EVEN
PIC	INCBIN "\MENU.196\CD196.PI1"
CHARSET	INCBIN "\MENU.196\FONT.PI1"
SETREZ	DC.B 27,"EPlease run in Medium Res...",00
	EVEN
TEXT
LAWTXT	DC.B " ...... ONE MEG MONSTERS ......    "  
	DC.B " AUTOMATION PRESENT MENU 196....     "
	DC.B "HELLO POMPEY PIRATES....   I HAVE HEARD RUMOURS OF YOU SLAGGING ME OFF....    "
	DC.B "I DON'T KNOW WHAT YOU HAVE SAID, BUT IF IT IS ABOUT ME SAYING - "
	DC.B '"THUNDER IS CRAP" '
	DC.B " - THEN I APPOLOGISE, AS I DIDN'T REALISE IT WAS A NEW THUNDER (THE ONE I SAW WAS CRAP!)...     "
	DC.B "HOWEVER, IF YOU SLAGGED ME OFF BECAUSE OF MY - DEREK EXPOSURE - THEN I DON'T BACK DOWN...    " 
	DC.B "I DON'T WANT A SLAGGING WAR WITH YOU BECAUSE THEY REALLY ARE POINTLESS (IE, YOU SLAG US, WE SLAG YOU, YOU SLAG US....  GET THE IDEA?)      "
	DC.B "NUFF SAID...  ON WITH THE SCROLL...     "
	DC.B "OZZ IS ALIVE AND KICKING (YES, I HAVE TALKED WITH HIM!)     "
	DC.B "SCROLLTEXTS ARE VERY DANGEROUS THINGS, DON'T YOU THINK?    "
	DC.B "FONT AND GRAFIX BY XERID, "
	DC.B "WITH ALL CODING AND MUSIC RIPPING BY ME! (THE LAW)       "
	DC.B "CHASE HQ IS ONE MEG BECAUSE IT IS FAR MORE PLAYABLE WHEN HELD IN RAM... "
	DC.B "I HAVE ALSO RELEASED A 512K BOOT VERSION...    ALSO, "
	DC.B "WHEN PLAYING IN 60HZ YOU DO SO AT YOUR OWN RISK - THE PROG WAS DESIGNED TO KNACKER UP IN 60HZ, BUT I "
	DC.B "HAVE AT LEAST GOT IT GOING...      "
	DC.B "BALLANCE OF POWER NEEDS THE DISK TO BE WRITE ENABLED...    CHECK YOU DON'T HAVE A VIRUS IN MEMORY FIRST!..."
	DC.B "WELL, ONLY THREE MORE TO GO BEFORE WE CHECK OUT OF THE NUMBERS "
	DC.B 'STARTING WITH "1" - 300 COMING SOON....  '
	DC.B "LET'S GET THE GREETS OUT OF THE WAY......   HI....  "
	DC.B "THE MEDWAY BOYS (HOW ABOUT A GREET?), THE POMPEY PIRATES (NICE BATMAN HACK - OURS COMING SOON), THE UNION (ALL MEMBERS - HOW ABOUT A GREET?), THE BLADERUNNERS (ALL MEMBERS - HOW ABOUT A GREET?), "
	DC.B "SEWER SOFTWARE (GREET ME, GUYS!), GUARDIANS OF LOGIK (AFTER ALL, I DO GREET YOU!), THE OVERLANDERS (NICE INTRO - BUT THE SCROLLER IS TOO FAST!!! - ALSO, HOW ABOUT A GREET ON A ONE FOR ONE BASIS?), "
	DC.B "THE LIGHT TEAM (WILL YOU GREET ME IN YOUR NEW INTRO?), HRFH, REVOLUTION, EMPIRE (THANKS FOR THE AUTOMATION GREET!), HOTLINE (AND BIRDY! - LIKEWISE!), DELIGHT, MAL, COL, BOB, DEAN, JON (THANX FOR THE UP-GRADE!), "
	DC.B "XERID, IRS (MARK/MIKE), SIMON, THE ORIGINAL SNOT GOBBLER (WHO IS THIS MAN?) AND OF COURSE TO YOU.....  "
	DC.B "I HAVE JUST DONE CHASE HQ...  LOAD IT TO SEE HOW IMPRESSED I WAS WITH THE PROTECTION...    R.N.C. ARE A JOKE....     "
	DC.B "DON'T BUY THIS DISK FROM ANYONE...  NONE OF THEM ARE FOR SALE...    "
	DC.B "TIME TO WRAP......    ...... ONE MEG MONSTERS......     NO, THAT WASN'T THE WRAP...  I'M STILL HERE....    "
	DC.B "OH, GOD..  THE SCROLLERS GONE WRONG...   I SAID....  WRA "
	DC.B $FF,$FF 

	EVEN

***************************
* END OF CODE............ *
***************************


