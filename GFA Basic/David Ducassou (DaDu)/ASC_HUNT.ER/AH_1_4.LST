'       ASCII HUNTER
'       v1.4
'
'
'
IF XBIOS(4)=0
  ALERT 3,"ASCII HUNTER by DADU. | |Please, |Increase resolution ! ",1," QUIT ",dummy%
  EDIT
ENDIF
'
RESERVE 20000
'
buffer_sect$=SPACE$(1500)
'
background_black!=FALSE
'
ligne_vide$=STRING$(79," ")
petit_vide$=STRING$(45," ")
'
piste%=0
secteur%=1
face%=0
sectors_nbr&=9
tracks_nbr&=79
' creator_present!=TRUE
creator_present!=FALSE
'
pos_c%=2                                !       position du curseur
'
DIM tt$(10)                             !       texte type
tt$(1)="to"
tt$(2)="you"
tt$(3)="for"
tt$(4)="by"
tt$(5)="tos"
tt$(6)="if"
tt$(7)="prg"
tt$(8)="did"
tt$(9)="the"
tt$(10)="that"
'
'
' GOTO no_specials_words
' tt$(1)="MASTER"
' tt$(2)="YO"
' tt$(3)="HARD"
' t$(4)="BOUTON"
' tt$(5)="AAA"
' tt$(6)="AAA"
' tt$(7)="AAA"
' tt$(8)="AAA"
' tt$(9)="AAA"
' tt$(10)="AAA"
' no_specials_words:
'
PRINT CHR$(27);"v";             !       d‚passement de texte autoris‚
'
aff_parametres_1
aff_parametres_2
aff_infos_keys
'
PRINT AT(1,4);" "
FOR i|=1 TO 15
  PRINT "    Press HELP for more informations"
NEXT i|
IF creator_present!=FALSE
  PRINT AT(1,23);"This soft cost ONLY 30 FFR, BUY IT !!!  (HELP for infos.)"
ENDIF
'
'
REPEAT
  t
  IF t=203 AND pos_c%>1
    DEC pos_c%
  ENDIF
  IF t=205 AND pos_c%<5
    INC pos_c%
  ENDIF
  '
  IF t=200                              !       fl‚che haut
    IF pos_c%=1 AND piste%<84
      INC piste%
    ENDIF
    IF pos_c%=2 AND secteur%<sectors_nbr&
      INC secteur%
    ENDIF
    IF pos_c%=3
      IF face%=0
        face%=1
      ELSE
        face%=0
      ENDIF
    ENDIF
    IF pos_c%=4
      INC sectors_nbr&
      IF sectors_nbr&>11
        sectors_nbr&=11
      ENDIF
    ENDIF
    IF pos_c%=5
      INC tracks_nbr&
      IF tracks_nbr&>85
        tracks_nbr&=85
      ENDIF
    ENDIF
  ENDIF
  '
  IF t=208                              !             bas
    IF pos_c%=1 AND piste%>0
      DEC piste%
    ENDIF
    IF pos_c%=2 AND secteur%>1
      DEC secteur%
    ENDIF
    IF pos_c%=3
      IF face%=0
        face%=1
      ELSE
        face%=0
      ENDIF
    ENDIF
    IF pos_c%=4
      DEC sectors_nbr&
      IF sectors_nbr&<1
        sectors_nbr&=1
      ENDIF
    ENDIF
    IF pos_c%=5
      DEC tracks_nbr&
      IF tracks_nbr&<0
        tracks_nbr&=0
      ENDIF
    ENDIF
  ENDIF
  '
  IF t=210                                      !       insert
    IF face%=0
      face%=1
    ELSE
      face%=0
    ENDIF
  ENDIF
  '
  '
  IF t$=" "
    GOSUB read_secteur
    '    aff_secteur
    GOSUB cherche
    GOSUB info_vide
    IF g_trouve_quelque_chose!=TRUE
      PRINT AT(1,23);"I have found some words:  ";truc$
    ENDIF
    GOSUB aff_secteur
  ENDIF
  '
  IF t=226        !       help
    help
  ENDIF
  '
  '   ******************  PASSAGE EN VERS. NON LIMITEE  *******************
  IF t=4          !   CONTROL D
    CLS
    FOR i|=1 TO 70
      PRINT "ceoratreoracratoorractroacro";
    NEXT i|
    INPUT "",code$ !  CREATOR
    code$=UPPER$(code$)
    IF LEFT$(code$,1)="C"
      IF RIGHT$(code$,1)="R"
        IF MID$(code$,2,5)="REATO"
          creator_present!=TRUE
        ENDIF
      ENDIF
    ENDIF
    aff_parametres_1
    aff_parametres_2
    aff_infos_keys
  ENDIF
  '
  IF t=187        !       F1
    '
    '   *********************  AUTOMATIC SEARCH  **********************
    '
    abort!=FALSE
    pos_c%=0
    GOSUB aff_parametres_2
    PRINT AT(1,23);"[ ] Reading sector       [ ] Searching ASCII words"
  ch_s_le_dsk:
    PRINT AT(11,2);"Sector: ";secteur%;" "
    '
    inverse_on
    PRINT AT(2,23);" "
    GOSUB read_secteur
    inverse_off
    PRINT AT(2,23);" "
    '
    GOSUB cherche
    IF g_trouve_quelque_chose!=TRUE
      '
      GOSUB aff_parametres_2
      '
      GOSUB info_vide
      PRINT AT(1,23);"I have found some words:  ";truc$
      PRINT CHR$(27)+"p";" Press anykey ... "+CHR$(27)+"q"
      GOSUB aff_secteur
    ELSE
      INC secteur%
      '
      t$=INKEY$
      IF t$=CHR$(27)
        abort!=TRUE
        GOTO end_of_search
      ENDIF
      '
      IF secteur%>sectors_nbr&
        secteur%=1
        INC piste%
        PRINT AT(1,2);"Track: ";piste%;" "
      ENDIF
      IF piste%<tracks_nbr&+1
        GOTO ch_s_le_dsk
      ELSE
        DEC piste%
        secteur%=sectors_nbr&
        GOSUB info_vide
        PRINT AT(1,24);CHR$(27)+"p";" End of this side, press anykey... "+CHR$(27)+"q"
        GOSUB flash_when_finish
        GOTO end_of_search
      ENDIF
    fin_ch:
    ENDIF
    '
    GOSUB flash_when_finish
    PRINT AT(1,24);CHR$(27)+"p";" To continue to search, press space ... "+CHR$(27)+"q"
    t
    GOSUB info_vide
    PRINT AT(50,20);"Last found:  ";piste%;",";secteur%;",";face%;"  "
    '
    IF t$=" "
      PRINT AT(1,23);"[ ] Reading sector       [ ] Searching ASCII words"
      INC secteur%
      IF secteur%>sectors_nbr&
        INC piste%
        secteur%=1
      ENDIF
      PRINT AT(1,24);ligne_vide$
      GOTO ch_s_le_dsk
    ENDIF
    '
  end_of_search:
    pos_c%=2
    aff_parametres_2
    info_vide
    PRINT AT(1,24);ligne_vide$
    IF abort!=TRUE
      info_sur_le_st("Search aborted...")
    ENDIF
    '
    '   ***********  END OF  AUTOMATIC SEARCH  *************
    '   ****************************************************
  ENDIF
  '
  '
  IF t=188      !       F2
    GOSUB infos_nbr
    aff_parametres_1
    VTAB 3
    aff_infos_keys
  ENDIF
  IF t=189      !       F3
    changer_les_mots
    aff_parametres_1
    VTAB 3
    aff_infos_keys
  ENDIF
  '
  IF t=191      !       F5
    background_black!=TRUE
    SETCOLOR 0,&H0
    SETCOLOR 3,&H555
  ENDIF
  IF t=192
    background_black!=FALSE
    SETCOLOR 0,&H555
    SETCOLOR 3,&H0
  ENDIF
  '
  GOSUB aff_parametres_2
  '
  ' pour quitter:
  ' CTRL C, ou F10, ou CTRL Q, ou ALT Q, ou Q  ou  q
  '
UNTIL t=3 OR t=196 OR t=17 OR t=158 OR t=81 OR t=113
'
fin
CLS
RESERVE -1
EDIT
'
> PROCEDURE read_secteur
  '  info_sur_le_st("Reading sector")
  ~XBIOS(8,L:V:buffer_sect$,L:0,W:0,W:secteur%,W:piste%,W:face%,W:1)
RETURN
> PROCEDURE aff_parametres_1      !       Title bar, Version numbers
  CLS
  '
  PRINT CHR$(27)+"p";
  IF creator_present!=FALSE
    PRINT " - ASCII HUNTER -  v0.94 Limited demo          31/03/1992, by DAvid DUcassou. "
  ELSE
    PRINT " - ASCII HUNTER -  v1.4 (selled, don't copy!)  31/03/1992, by DAvid DUcassou. "
  ENDIF
  inverse_off
  '
RETURN
> PROCEDURE aff_parametres_2
  IF pos_c%=1
    inverse_on
    PRINT AT(1,2);"Track: ";piste%;" "
    inverse_off
  ELSE
    PRINT AT(1,2);"Track: ";piste%;"  "
  ENDIF
  IF pos_c%=2
    inverse_on
    PRINT AT(11,2);"Sector: ";secteur%;" "
    inverse_off
  ELSE
    PRINT AT(11,2);"Sector: ";secteur%;"  "
  ENDIF
  IF pos_c%=3
    inverse_on
    PRINT AT(23,2);"Side: ";face%;" "
    inverse_off
  ELSE
    PRINT AT(23,2);"Side: ";face%;" "
  ENDIF
  IF pos_c%=4
    inverse_on
    PRINT AT(32,2);"Sectors #: ";sectors_nbr&;" "
    inverse_off
  ELSE
    PRINT AT(32,2);"Sectors #: ";sectors_nbr&;"  "
  ENDIF
  IF pos_c%=5
    inverse_on
    PRINT AT(46,2);"Tracks #: ";tracks_nbr&;" "
    inverse_off
  ELSE
    PRINT AT(46,2);"Tracks #: ";tracks_nbr&;"  "
  ENDIF
  '
RETURN
'
> PROCEDURE aff_infos_keys
  aff_infos_keys_htab
  PRINT "F1   automatic search"
  aff_infos_keys_htab
  PRINT "F2   info. sect/trk #"
  aff_infos_keys_htab
  PRINT "F3   change the words"
  aff_infos_keys_htab
  PRINT "F5   black screen"
  aff_infos_keys_htab
  PRINT "F6   normal screen"
  aff_infos_keys_htab
  PRINT "F10  quit"
  aff_infos_keys_htab
  PRINT "ESC  stop auto. search"
  aff_infos_keys_htab
  PRINT "SPACE  read a sector"
  aff_infos_keys_htab
  PRINT "INSERT  change side"
  aff_infos_keys_htab
  PRINT "HELP  infos author"
  PRINT
  aff_infos_keys_htab
  PRINT "ARROW KEYS  change parameters"
  PRINT
  PRINT
  aff_infos_keys_htab
  PRINT "     Realized by"
  aff_infos_keys_htab
  PRINT "DADU-SOFTWARE  ST dept."
RETURN
> PROCEDURE aff_infos_keys_htab
  HTAB 50
RETURN
'
> PROCEDURE aff_secteur
  adr_a_lire%=VARPTR(buffer_sect$)
  '
  '
  '     16*16=256
  '
  FOR l%=5 TO 21
    VTAB l%
    PRINT petit_vide$
    FOR c%=5 TO 37                      !       16
      LOCATE c%,l%
      PRINT CHR$(PEEK(adr_a_lire%))
      INC adr_a_lire%
    NEXT c%
  NEXT l%
  '
RETURN
'
> PROCEDURE cherche
  '
  '  info_sur_le_st("Searching ASCII words")
  inverse_on
  PRINT AT(27,23);" "
  '
  g_trouve_quelque_chose!=FALSE
  truc$=""
  '
  FOR nb_txt%=1 TO 10
    nl1%=INSTR(buffer_sect$,tt$(nb_txt%),0)
    nl2%=INSTR(buffer_sect$,UPPER$(tt$(nb_txt%)),0)
    '
    IF nl1%<>0 OR nl2%<>0
      g_trouve_quelque_chose!=TRUE
      truc$=truc$+" "+tt$(nb_txt%)
    ENDIF
  NEXT nb_txt%
  '
  inverse_off
  PRINT AT(27,23);" "
  '
RETURN
'
> PROCEDURE info_sur_le_st(information$)
  info_vide
  PRINT AT(1,23);"              ";information$
RETURN
> PROCEDURE info_vide
  PRINT AT(1,23);ligne_vide$
RETURN
> PROCEDURE help
  CLS
  PRINT " - ASCII HUNTER -"
  PRINT
  PRINT "Program made by       DAVID DUCASSOU"
  PRINT "                      ROUTE DE MONSEGUR"
  PRINT "                      40700 HAGETMAU"
  PRINT "                       FRANCE"
  PRINT "French phone: 58.79.43.01   Minitel: 3614 RTEL2, B.A.L. DADU"
  PRINT
  PRINT " This prg allow you to read texts which are on a disk, by ";
  PRINT "searching several"
  PRINT "(10) words, or numbers, in the same time !."
  PRINT
  PRINT " Your can  read the texts which are ";
  PRINT "hidden on the demos disks (there were only readable with a disk editor)."
  PRINT "Eg. :  UNION, MIND BOMB, SYNTAX TERROR, ... ..."
  PRINT " The words are searched in small letters and capitals, they are set"
  PRINT "in english."
  PRINT
  IF creator_present!=TRUE
    PRINT "Thank you for having buy this soft."
    PRINT
    PRINT "Please, do not copy this soft to your friends. Don't be a thief !."
  ENDIF
  PRINT
  inverse_on
  PRINT " Press any key... "
  inverse_off
  t
  '
  IF creator_present!=FALSE
    CLS
    PRINT " - ASCII HUNTER -    2/2"
    PRINT
    PRINT
    PRINT "If you think this soft is usefull, and you want gratify the author to allow him to continue to make utilities on ST, then :"
    PRINT
    PRINT " To receive the 1.? ( the latest ) UNLIMITED version of this soft, sent me :"
    PRINT "two 3p 1/2 disks ( with yours softs or a demo ),"
    PRINT "30 French francs,"
    PRINT "And one stamped ( 4 Fr for frenchs ) and adressed envelop."
    PRINT "Tell me in the letter that you want the registred version of ASCII HUNTER."
    PRINT " I'll put my others ( numerous ) freewares softs on the second disk"
    PRINT "Eg: TEXT GRAPHIST, DUP-SHOW, SAmple SHOwer, Hiddens Files Hunter, ....."
    PRINT " And you will be informed of ASCII HUNTER's releases."
    PRINT
    PRINT "Sent me your ideas for improvements. If you wish and answer then sent also a Stamped and Adressed Envelop."
    PRINT "I have got most of the latest demos."
    PRINT
    PRINT "If you are a freeware or shareware author, contact me to swap our products !."
    PRINT
    inverse_on
    PRINT " Press any key... "
    inverse_off
    t
  ENDIF
  '
  '
  aff_parametres_1
  aff_parametres_2
  aff_infos_keys
RETURN
> PROCEDURE t
  t=INP(2)
  t$=CHR$(t)
RETURN
'
> PROCEDURE changer_les_mots
  '  tt$(1)="to"
  CLS
  PRINT "---   ASCII HUNTER. Change the searched words   ---"
  PRINT
  PRINT "Type RETURN to keep it, else, type the new word in small letters."
  FOR i%=1 TO 10
    '
    PRINT "No ";i%;":      ";tt$(i%);" / ";UPPER$(tt$(i%))
    INPUT "New word ->",rep$
    '
    IF rep$<>""
      IF creator_present!=FALSE
        ALERT 1,"Sorry, this limited demo |version don't allow it. |You use this soft, you |may but it !...",1," YEAH ",dummy&
      ELSE
        tt$(i%)=rep$
      ENDIF
    ENDIF
    '
  NEXT i%
RETURN
> PROCEDURE infos_nbr
  CLS
  PRINT "-------  ASCII HUNTER,  TRACKS and SECTORS NUMBERS (#)  INFORMATIONS   --------"
  PRINT
  PRINT "Actual sectors number = ";sectors_nbr&
  PRINT
  PRINT " Habits sectors numbers:"
  PRINT "Demos disks = 10"
  PRINT "GEM applications (standart), PC disks = 9"
  PRINT
  PRINT
  PRINT
  PRINT "Actual maximum tracks number =  ";tracks_nbr&+1;"  (0 to ";tracks_nbr&;")"
  PRINT
  PRINT " Habits tracks numbers:"
  PRINT "Demos disks = 81"
  PRINT "GEM applications (standart), PC disks = 79"
  PRINT
  PRINT "Most of the Disk Drives can't read more than 80 tracks (0 to 79)."
  PRINT
  PRINT
  PRINT
  PRINT "Press anykey..."
  t
RETURN
'
> PROCEDURE fin
  ~MFREE(buffer_sect%)
RETURN
> PROCEDURE inverse_on
  PRINT CHR$(27)+"p"
RETURN
> PROCEDURE inverse_off
  PRINT CHR$(27)+"q"
RETURN
'
> PROCEDURE flash_when_finish
  '     D. DUCASSOU, 5/6/91
  '
  REPEAT
    '
    IF INKEY$<>""
      GOTO fast_press
    ENDIF
    VSYNC
    SETCOLOR 0,&H0
    SOUND 1,15,2,2,10
    PAUSE 10
    '
    VSYNC
    SETCOLOR 0,&H666
    SOUND 1,15,2,3,10
    PAUSE 10
  UNTIL INKEY$<>""
  SOUND 0,0,0,0,0
  '
fast_press:
  IF background_black!=TRUE
    SETCOLOR 0,&H0
    SETCOLOR 3,&H555
  ENDIF
RETURN
' mettre avec mutil l'abr‚viation du nom du mec
PRINT " *** I NEED $ *** "
