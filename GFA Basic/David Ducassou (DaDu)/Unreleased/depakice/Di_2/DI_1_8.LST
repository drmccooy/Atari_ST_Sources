'               - DEPAKICE -
'
'
' Encod‚:          " REALISE PAR DUCASSOU DAVID "
little_protection$="XSFBMJTFXQBSXEVDBTTPVXEBWJEX"
'
IF XBIOS(4)=0
  PRINT CHR$(7)
  dummy&=FORM_ALERT(1,"[3][|Sorry, but |DEPAKICE (by DADU) need |a highter resolution.][    QUIT    ]")
  EDIT
ENDIF
'
' 40000 sinon UIS3 fait error RAM. J'y comprends rien!...
RESERVE 40000
'
path_s$="\*.*"
path_d$="\*.*"
depack_only!=FALSE
DIM reg%(15)              !       les registres
title$=CHR$(27)+"p"+"        DEPAKICE v1.8  01/01/1992, DADU-SOFTWARE       "+CHR$(27)+"q"
'
PRINT CHR$(27);"v";             !       d‚passement de texte autoris‚
'
dummy&=FORM_ALERT(1,"[1][|DEPAKICE is |a shareware product.][    OK    ]")
menu:
CLS
PRINT title$
PRINT "        ----------------------------------------"
PRINT
PRINT "DADU-DEPAKICE allow you to depack packed datas files."
PRINT
PRINT "Author: DAvid DUcassou            ";CHR$(27);"p This soft is SHAREWARE ";CHR$(27);"q (press HELP)"
PRINT "        ROUTE DE MONSEGUR"
PRINT "        40700 HAGETMAU"
PRINT "         FRANCE           (join an SAE)"
PRINT
PRINT
' PRINT "RAM free  = ";FRE(1);" bytes"
PRINT "+------------- functions --------------------- ----- ---- --- -- -"
PRINT "| Source pathname      =  ";path_s$;"   ( Press 1 to change it )"
PRINT "| Destination pathname =  ";path_d$;"   ( Press 2 to change it )"
PRINT "|"
IF depack_only!=FALSE
  PRINT "| RETURN Decompact then save"
ELSE
  PRINT "| RETURN Decompact but do not save"
ENDIF
PRINT "|"
PRINT "| SPACE  To continue ( CONTROL Q  to quit ),"
PRINT "| HELP   For more infos about the author and  DADU-DEPAKICE."
PRINT "+--------------------------------------------- ----- ---- --- -- -"
PRINT
PRINT "It is totally forbidden to sell this software without the agreement of"
PRINT "the author ( You're warned, PRESSIMAGE !)."
'
REPEAT
  t
  IF t$="Q" OR t%=17 OR t%=3 OR t%=158
    CLS
    RESERVE -1
    EDIT
  ENDIF
  IF t$="D" OR t%=226 OR t%=187
    GOSUB infos_auteur
    GOTO menu
  ENDIF
  IF t%=199
    SETCOLOR 0,&H333
  ENDIF
  IF t%=13
    IF depack_only!=FALSE
      depack_only!=TRUE
    ELSE
      depack_only!=FALSE
    ENDIF
    GOTO menu
  ENDIF
  IF t$="1"
    PRINT
    INPUT "New source pathname   >",path_s$
    GOTO menu
  ENDIF
  IF t$="2"
    PRINT
    INPUT "New source pathname   >",path_d$
    GOTO menu
  ENDIF
  '
UNTIL t$=" "
'
cont:
CLS
PRINT title$;"   FILE TO DEPACK"
FILESELECT path_s$,"",file$
' file$="F:\*.SPL"
IF file$=""
  GOTO menu
ENDIF
'
IF depack_only!=FALSE
  CLS
  PRINT title$;"   SAVE AS ..."
  FILESELECT path_d$,"",file_dest$
  ' file_dest$="F:\AA"
  IF file_dest$=""
    GOTO menu
  ENDIF
ENDIF
'
'       *************************************
CLS
PRINT title$
PRINT
PRINT
IF depack_only!=FALSE
  PRINT "Working with:  ";file$;"  ->  ";file_dest$
ELSE
  PRINT "Working with:  ";file$
ENDIF
PRINT
GOSUB depack
'
IF depack_only!=FALSE
  IF packed_size%<>0
    PRINT AT(1,11);"Please wait, SAVING ..."
    DEFMOUSE 2
    BSAVE file_dest$,sample%,taille%
    DEFMOUSE 0
    PRINT AT(1,11);"File saved.            "
  ENDIF
ENDIF
'
~MFREE(sample%)
PRINT AT(1,23);"Press any key ..."
t
GOTO cont
' GOTO menu
'
> PROCEDURE infos_auteur
  CLS
  PRINT title$;"   1/3"
  PRINT
  PRINT "DEPAKICE is now able to depack:"
  PRINT
  PRINT
  PRINT "HEADER (4 bytes)         PACKER                  AUTHOR"
  PRINT
  PRINT "    Ice!                 PACK ICE v2.12          AXE from DELIGHT"
  PRINT "    ICE!                 PACK ICE v2.40          AXE from SUPERIOR"
  PRINT "    FIRE                 PACK FIRE v2.0          AXE from DELIGHT"
  PRINT "    ATOM                 ATOMIK CRUNCHER v3.3    ALTAIR from VMAX"
  PRINT
  PRINT
  PRINT "Little info about headers at the end of packed datas files:"
  PRINT "    POPI                 POMPEY PACKER           ALIEN from PP"
  PRINT "    JEK!                 JEK PACKER or JAM v2.3  SHARAZ JEK"
  PRINT
  PRINT "Warning! DEPAKICE is not able to depack those 2 formats."
  PRINT AT(8,23);"Press anykey..."
  t
  CLS
  PRINT title$;"   2/3"
  PRINT
  PRINT "       Why to be a ShareWare donator :"
  PRINT
  PRINT "* You will be a registred user."
  PRINT "* You will be informed of releases, and get some next."
  PRINT "* If you want to depack .PRG files, and keep it executable,  then sent me a disk containing the .PRG file to depack, and a stamped and adressed envelop."+CHR$(0)
  PRINT "(If the disk is not full, put some shareware or freewares or intros onto it)."
  PRINT "I am also able to depack a lot of packed types, and save as executable files."
  PRINT "This service will be free !."
  PRINT "* I am a fan of packers, so I own most of the packers that exist."
  PRINT "* You will make me happy !."
  PRINT
  PRINT "       How to become a ShareWare donator and a DADU-DEPAKICE registred user :"
  PRINT
  PRINT "* Sent me $10 or 50 FFr (French FRancs) or an equivalent sum of money,"
  PRINT "* One 3p 1/2 disk containing shareware and/or freewares"
  PRINT "  or some money to buy me one diskette ($1 or 5 FFr),"
  PRINT "* A letter with your comments."
  PRINT
  PRINT AT(8,23);"Press anykey..."
  t
  CLS
  PRINT title$;"   3/3"
  PRINT
  PRINT "If you make Share Wares or Freeware, contact me !."
  PRINT "I have got the latest demos and disks magazines."
  PRINT "To get my list, sent me 1 diskette (copy anything onto it) + a Stamped and Adressed Envelope."+CHR$(0)
  PRINT
  PRINT "If you write me to give comments, please include a stamped (or Int. Reply Cupon) and adressed envelop to get an answer."+CHR$(0)
  PRINT
  PRINT "DEPAKICE was made with GfA BASIC v3.5f and ASM routs."
  PRINT
  PRINT "Hi to: SHIZUKA (thanks), IMPACT (AYOROS, TOXIC, HELRIZER, ...), I.F.A.,"+CHR$(0)
  PRINT "THE CONCEPTORS, TSK, DNT (AXIOM, FLIPS), MAGGIE'S ARTICLES WRITERS, ...."
  PRINT "and ALL the  share ware/free ware  spreaders."
  PRINT AT(8,23);"Press anykey..."
  t
RETURN
> PROCEDURE t
  t%=INP(2)
  t$=UPPER$(CHR$(t%))
RETURN
> PROCEDURE depack
  header$=STRING$(4,0)
  unpacked_size$=STRING$(4,0)
  packed_size$=STRING$(4,0)
  unpacked_size%=0
  packed_size%=0
  taille%=0
  '
  PRINT AT(1,23);"OPENING ...     "
  OPEN "I",#1,file$                              ! prends la longueur du spl
  '
  taille%=LOF(#1)
  '
  BGET #1,V:header$,4
  IF header$="ICE!" OR header$="Ice!"
    BGET #1,V:packed_size$,4
    BGET #1,V:unpacked_size$,4
    '    unpacked_size%=FN asc_to_hex_to_dec(unpacked_size$)
    unpacked_size%=CVL(unpacked_size$)
    sample%=MALLOC(unpacked_size%+1024)
    taille%=unpacked_size%
  ELSE IF header$="FIRE"
    BGET #1,V:packed_size$,4
    BGET #1,V:unpacked_size$,4
    unpacked_size%=CVL(unpacked_size$)
    sample%=MALLOC(unpacked_size%+1024)
    taille%=unpacked_size%
  ELSE IF header$="ATOM"
    BGET #1,V:unpacked_size$,4
    BGET #1,V:packed_size$,4
    unpacked_size%=CVL(unpacked_size$)
    sample%=MALLOC(unpacked_size%+1024)
    taille%=unpacked_size%
  ENDIF
  CLOSE #1
  '
  packed_size%=CVL(packed_size$)
  '
  IF packed_size%=0
    PRINT AT(1,6);"File length  =  ";taille%;" bytes."
  ELSE
    PRINT AT(1,6);"File length  =  ";packed_size%;" bytes,"
  ENDIF
  IF header$<>"ICE!" AND header$<>"ATOM" AND header$<>"Ice!" AND header$<>"FIRE"
    header3$=LEFT$(header$,3)
    PRINT AT(1,23);"                "
    PRINT AT(1,8);CHR$(7);
    '
    PRINT "SORRY, I CAN NOT UNPACK THIS FILE."
    IF header3$="LSD"
      PRINT "Use the AUTOMATION PACKER v2.3 or v2.5"
    ELSE IF header3$="LZH"
      PRINT "Try to use LZH.TTP, or JAM PACKER v3 or v4"
    ELSE IF header3$="LZW"
      PRINT "Try to use JAM PACKER v4"
    ELSE IF header3$="JEK"
      PRINT "Try to use JAM PACKER v2.3"
    ELSE
      PRINT "I DO NOT RECOGNIZE IT."
    ENDIF
    PRINT
    PRINT "The header of this file is: ";CHR$(27);"p";header$;CHR$(27);"q"
    GOTO exit
  ELSE
    PRINT AT(30,6);"unpacked size = ";taille%;" bytes."
  ENDIF
  '
  PRINT AT(1,23);"LOADING ...     "
  '
  BLOAD file$,sample%
  '
  FOR iik&=0 TO 15
    reg%(iik&)=0
  NEXT iik&
  '
  IF header$="Ice!"
    INLINE dpak_ice2_12%,590
    reg%(8)=sample%
    PRINT AT(1,8);"This file seem be packed with:  PACK-ICE v2.12"
    IF depack_only!=FALSE
      PRINT AT(1,23);"DEPACKING ..."
      RCALL dpak_ice2_12%,reg%()
    ENDIF
    PRINT AT(1,23);"             "
  ELSE IF header$="ICE!"
    INLINE dpak_ice%,380
    reg%(8)=sample%
    PRINT AT(1,8);"This file seem be packed with:  PACK-ICE v2.40"
    IF depack_only!=FALSE
      PRINT AT(1,23);"DEPACKING ..."
      RCALL dpak_ice%,reg%()
    ENDIF
    PRINT AT(1,23);"             "
  ELSE IF header$="FIRE"
    '   FIREUNP2.S
    INLINE dpak_ice%,446
    reg%(8)=sample%
    PRINT AT(1,8);"This file seem be packed with:  PACK-FIRE v2.0"
    IF depack_only!=FALSE
      PRINT AT(1,23);"DEPACKING ..."
      RCALL dpak_ice%,reg%()
    ENDIF
    PRINT AT(1,23);"             "
  ELSE IF header$="ATOM"
    ' … propos du INLINE qui suit:  RESTORE_M = 0       PIC_ALGO=0
    INLINE dpak_atom_3_3%,390
    reg%(8)=sample%
    PRINT AT(1,8);"This file seem be packed with:  ATOMIK CRUNCHER v3.3"
    IF depack_only!=FALSE
      PRINT AT(1,23);"DEPACKING ..."
      RCALL dpak_atom_3_3%,reg%()
    ENDIF
    PRINT AT(1,23);"             "
  ENDIF
exit:
  '
RETURN
