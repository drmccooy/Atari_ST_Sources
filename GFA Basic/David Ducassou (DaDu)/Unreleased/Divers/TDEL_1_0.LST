'       DADU-TOTal-DELeter
'
IF XBIOS(2)=0
  ALERT 3,"|DADU-TOTal-DELeter prefer |Medium resolution. |",1,"GO ON|QUIT",choix%
  IF choix%=2
    EDIT
  ENDIF
ENDIF
'
'
vide$=SPACE$(30)
chemin$="A:\*.*"
'
'
choose:
CLS
aff_titre
PRINT
PRINT "             This product is SHAREWARE, give me some disks!"
PRINT
PRINT
PRINT "1 > Totally delete a file"
PRINT
PRINT "HELP  >  Informations, author adress, description,..."
PRINT
PRINT "CTRL Q  >  Quit"
t
'
' PRINT t%
IF t$="1"
  tot_del_file
ELSE IF t%=226
  GOSUB help
ELSE IF t%=17 OR t%=3
  EDIT
ENDIF
GOTO choose
'
> PROCEDURE tot_del_file
  '
encore_et_encore:
  CLS
  aff_titre
  PRINT vide$;"Totally delete a file"
  '
  FILESELECT chemin$,"",file_to_del$
  '
  IF file_to_del$<>""
    '
    CLS
    aff_titre
    PRINT
    OPEN "I",#1,file_to_del$
    '
    length%=LOF(#1)
    '
    CLOSE #1
    '
    PRINT "I will delete :  ";file_to_del$;"  ";length%;" Kb"
    PRINT
    PRINT
    PRINT
    fre%=FRE()
    PRINT "Ram free = ";fre%
    '
    IF (length%+5000)<fre%
      GOSUB full_0
      '
    ELSE
      PRINT CHR$(7)
      PRINT "Sorry, not enough ram free to kill this file"
    ENDIF
    '
    '
    PRINT
    PRINT "Press any key to come back to the file selector..."
    t
    chemin$=FN give_path$(file_to_del$)
    GOTO encore_et_encore
    '
  ENDIF
  '
RETURN
> PROCEDURE full_0
  '
  PRINT "Enough ram free avaible, I will use the BSAVE method."
  PRINT
  '
  full_of_0%=MALLOC(length%)
  IF full_of_0%>0
    '
    PRINT "Zeroing the ram file..."
    FOR dummy%=full_of_0% TO full_of_0%+length%
      POKE dummy%,0
    NEXT dummy%
    '
    clear_previous_text_line
    PRINT "I will totally delete the file, press ESC to abort or any other key to go on."
    t
    IF t%<>27
      clear_previous_text_line
      PRINT "Totally deleting:  ";file_to_del$;" ..."
      '
      '
      '   Fill the file with a lot of 0
      '   and then "kill it"
      '
      BSAVE file_to_del$,full_of_0%,length%
      KILL file_to_del$
      '
      clear_previous_text_line
      PRINT file_to_del$;"  was totally deleted."
    ELSE
      PRINT "Ok, file not deleted."
    ENDIF
  ELSE
    PRINT CHR$(7)
    PRINT "MALLOC error"
  ENDIF
  ~MFREE(full_of_0%)
RETURN
'
> PROCEDURE help
  CLS
  aff_titre
  PRINT
  PRINT " This prg can totally delete a file:"
  PRINT "First it fill it of 0, then it delete it from the F.A.T."
  PRINT "Then you CAN'T RECOVER ANY PART of the file ( for example with Mutil 2 )."
  PRINT
  PRINT "This prg was programmed with GFA BASIC v3.50F"
  PRINT
  PRINT "The author:  DUCASSOU DAVID            My Phone number: 58.79.43.01"
  PRINT "             ROUTE DE MONSEGUR"
  PRINT "             40700 HAGETMAU"
  PRINT "              FRANCE"
  PRINT
  PRINT "If you write me and wish an answer, join an envelop with an Int. Reply Cupon."
  PRINT
  PRINT
  PRINT "If you use this software, then give some 3p 1/2 disks with freeware or shareware softs ( Ex: 4 disks ). Thank you."+CHR$(0)
  PRINT "You will then be informed of releases."
  t
RETURN
'
> PROCEDURE clear_previous_text_line
  PRINT CHR$(27);"A"+CHR$(27);"l"
RETURN
> PROCEDURE t
  t%=INP(2)
  t$=UPPER$(CHR$(t%))
RETURN
> PROCEDURE inverse_on
  PRINT CHR$(27)+"p";
RETURN
> PROCEDURE inverse_off
  PRINT CHR$(27)+"q";
RETURN
> FUNCTION give_path$(path_entire$)
'     by DAVID DUCASSOU, 18/5/91
'
LOCAL path_entire%,c%,c$
'
path_entire%=LEN(path_entire$)
c%=path_entire%+1
'
REPEAT
  DEC c%
  c$=MID$(path_entire$,c%,1)
UNTIL c$="\"
RETURN LEFT$(path_entire$,c%)
ENDFUNC
'
PROCEDURE aff_titre             !       Titre, date et tout ‡a...
inverse_on
PRINT " DADU-TOTal-DELeter  v1.0  May 18th 1991, by DAVID DUCASSOU                    "
inverse_off
RETURN
