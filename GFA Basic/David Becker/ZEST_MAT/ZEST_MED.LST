'
' ZeST Alternative Interface
' Copyright 1991, David Becker
' Compuserve 73030,3562 / GEnie mail to D.Becker8
' ------------------------------------------------------
' 11/25/91
'
' I have converted David Becker's BRILLIANT ZeST Interface Routines
' to Medium Resolution (Soft gray is personal preference,but can easily
' be changed)
'
' Special thanks to David Becker for his programming contributions and
' especially his "knowledge sharing" attitude.
'
' And Dave, I'd really LOVE to have the source code for AutoZeST
' so I wouldn't have to do all this manually! :)
'
'                                          Gary Dooms
'                                          Delphi mail to GDOOMS
'
' ---------------------------------------------------------------------
' **** NOTE! ****
' I have switched around the color palette in order to draw with
' lightest gray. Remember to return to original color palette on
' termination of your program.
'
'  ! initialize palette
SETCOLOR 0,6,6,6   ! lightest gray
SETCOLOR 1,5,5,5   ! light gray
SETCOLOR 2,7,7,7   ! white
SETCOLOR 3,0,0,0   ! Black
'
DEFFILL 2,2,8
PBOX 0,0,639,200   ! set background light gray
'
' --------------- Routine Examples -------------------
'
'   Just call the procedure with the top x,y and bottom x,y coorordinates
'
@zest_button(40,30,600,140)
@zest_window(80,50,160,100)
@zest_button(180,50,260,100)
@zest_info_box(280,50,360,100)
@zest_text_box(380,50,460,100)
@zest_line_box(480,50,560,100)
@zest_info_box(80,105,160,115)
@zest_info_box(180,105,260,115)
@zest_info_box(280,105,360,115)
@zest_info_box(380,105,460,115)
@zest_info_box(480,105,560,115)
@zest_button(220,8,430,23)
@zest_button(180,160,462,175)
GRAPHMODE 3                            !White on Gray background
TEXT 229,18,"Examples of all routines"
GRAPHMODE 2                            !Black on Gray background
TEXT 97,113,"Window"
TEXT 197,113,"Button"
TEXT 288,113,"Info Box"
TEXT 388,113,"Text Box"
TEXT 488,113,"Line Box"
TEXT 190,170,"Press any key to return to editor"
REPEAT
UNTIL LEN(INKEY$)
EDIT
'
' **************** ZeST Medium Res Routines *********************
'
PROCEDURE zest_button(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  DEFFILL 0,2,8
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  DEFFILL 2,2,8
  PBOX upper_x%+1,upper_y%+1,lower_x%,lower_y%
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE lower_x%,upper_y%+1,lower_x%+1,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%,lower_y%
RETURN
PROCEDURE zest_button_press(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  GET upper_x%-1,upper_y%-1,lower_x%+1,lower_y%+1,button$
  GET upper_x%+1,upper_y%+1,lower_x%-2,lower_y%-2,shift_button$
  PUT upper_x%+2,upper_y%+2,shift_button$
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  COLOR 0
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE upper_x%,lower_y%+1,lower_x%,lower_y%+1
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%+1
  COLOR 1
  DO
    SHOWM
  LOOP UNTIL MOUSEK=0
  PAUSE 3
  PUT upper_x%-1,upper_y%-1,button$
RETURN
PROCEDURE zest_info_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  DEFFILL 2,2,8
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  LINE upper_x%+1,upper_y%,upper_x%+1,lower_y%
  COLOR 0
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%
  COLOR 1
RETURN
PROCEDURE zest_text_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  DEFFILL 1,2,8
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%+1,lower_x%,upper_y%+1
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  LINE upper_x%+1,upper_y%,upper_x%+1,lower_y%
  COLOR 0
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%
  COLOR 1
  DEFFILL 3,2,8
  PBOX upper_x%+2,upper_y%+1,lower_x%-1,lower_y%-1
RETURN
PROCEDURE zest_horiz_line(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  LINE upper_x%,upper_y%,lower_x%,lower_y%
  COLOR 0
  LINE upper_x%,upper_y%+1,lower_x%,lower_y%+1
  COLOR 1
RETURN
PROCEDURE zest_vert_line(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,2
  LINE upper_x%,upper_y%,lower_x%,lower_y%
  DEFLINE 1,2
  COLOR 0
  LINE upper_x%+2,upper_y%,lower_x%+2,lower_y%
  COLOR 1
RETURN
PROCEDURE zest_line_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  COLOR 0
  BOX upper_x%,upper_y%,lower_x%,lower_y%
  COLOR 1
  LINE upper_x%-1,upper_y%,upper_x%-1,lower_y%
  LINE upper_x%+2,lower_y%-1,lower_x%-2,lower_y%-1
  LINE lower_x%-1,upper_y%+2,lower_x%-1,lower_y%-2
  LINE upper_x%-1,upper_y%-1,lower_x%,upper_y%-1
RETURN
PROCEDURE zest_window(upper_x%,upper_y%,lower_x%,lower_y%)
  ' title bar
  GOSUB zest_button(upper_x%,upper_y%,lower_x%,upper_y%+11)
  ' window area
  GOSUB zest_button(upper_x%,upper_y%+12,lower_x%,lower_y%)
  ' close button
  GOSUB zest_button(upper_x%+5,upper_y%+2,upper_x%+18,upper_y%+9)
RETURN
