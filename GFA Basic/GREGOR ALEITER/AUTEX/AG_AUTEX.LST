Info:
Data " GFABASIC 2.2 / AG_AUTEX.PRG                          Nov. 88     VERS 1.00. AG "
Data
Data "                      ENTWICKLUNGSBöRO GREGOR ALEITER MöNCHEN            "
Data "                                 TEL 089\502 51 25                       "
Data
Data "             'AUTO'-Programme in gewÅnschter Reihenfolge ausfÅhren       "
Data
Data "                      BENôTIGT WIRD DAZU DIE DATEI AG_AUTO.INF           "
Data "                                IM ORDNER 'AG_AUTO'                      "
Data
Data "                  DIE AUFZURUFENDEN PROGRAMME SIND DER REIHE NACH        "
Data "                                    EINZUTRAGEN                          "
Data
Data "                DIE DATEI KANN MIT JEDEM TEXTEDITOR ERSTELLT WERDEN      "
Data "            !!!           DIE LETZTE ZEILE OHNE RETURN              !!!  "
Data
Data "     fÅr Programme wie FILESELECT und FOLDERXX.PRG die im '\AUTO' suchen,"
Data "              kînnen die gepatchten Programme nachgeliefert werden.      "
Data
Data "              DIESE VERSION KANN FREI BENUTZT UND WEITERGEGEBEN WERDEN   "
Data "             Bedingung:  der Name 'AG_AUTEX' darf nicht geÑndert werden  "
Data  ***
Reserve 10000
' ---
@String_init(*Pfad_ag_auto$,*Datei_ag_autex.inf$,*Umgebung$,*Ende_data$)
@Bild_init(*B_sync_modus_adr.%,*B_sync_50_hz%,*B_sync_60_hz%)
' ---
If Xbios(4)<2                        ! im Colormodus nur Mittlere Auflîsung
  @Setz_mittel_aufl.                 ! bei Bedarf weglassen
Endif
' ---
' Im Colormodus wichtig sonst Zufallsvorder/hintergrund
@4_farb_normal(*Flg.invers!)
' ---
' Hier alles fÅr NEC- MULTISYNC im HIres- Modus unwirksam.
Spoke B_sync_modus_adr.%,B_sync_60_hz%
' Spoke B_sync_modus_adr.%,B_sync_50_hz%
Sdpoke &H448,0                       !60 Hz
Sdpoke &H44A,1                       !mittlere Auflîsung beim nÑchsten Boot
' ---
' Beim Autostart - Programm wird die Peripherie Åber Halbleiterrelais eingeschaltet.
' Aus diesem Grund sind meine Floppys nicht bekannt, also ich habe 2 StÅck.
Slpoke &H4C2,Lpeek(&H4C2) Or &X11    !Floppy A+B
Sdpoke &H4A6,2                       !Anzahl Disk Laufwerke
' ---
@Print_info                          !das man mich  kennenlernt.
' ---
' Noch'n kleiner Unsinn, sollte man immer mit 'fsfirst' abfragen
' Exist funktioniert nich mit leerem Ordner                  !!!
If Exist(Pfad_ag_auto$+"*.*")=True
  Chdir Pfad_ag_auto$
Else
  ' Es gibt viele GrÅnde die eingebauten Funktionen nicht zu benutzen
  ' Es ist unbedingt zu beachten daû AES UND VDI noch nicht vorhanden sind
  ' also keine Alert's oder Basic-Fehlermeldungen
  @Cconws(Chr$(7)+"AusfÅhrung der anderen Programme nur aus Ordner 'AG_AUTO' zulÑssig")
  Pause 100
  @Ende
Endif
' ---
@Zaehl_ag.inf(*Anzahl%)              !Anzahl Autoprogramme
Void Fre(0)
If Anzahl%>0
  @Lade_ag.inf(Anzahl%)
  Void Fre(0)
  @Prg_aufruf
Endif
@Ende
' ------------------------------------------------------------------------------
Procedure Bild_init(B_sync_modus_adr.str.%,B_sync_50_hz_str.%,B_sync_60_hz_str.%)
  *B_sync_modus_adr.str.%=&HFF820A    ! Register Video anzeige   vgl. Intern S. 75
  *B_sync_50_hz_str.%=&X11111110
  *B_sync_60_hz_str.%=&X11111100
Return
' ------------------------------------------------------------------------------
Procedure String_init(Pfad_ag_auto_str.%,Datei_ag_autex.inf_str.%,Umgebung_str.%,Ende_data_str.%)
  Pfad_ag_auto$="\AG_AUTO\"         !fÅr Chdir ja keine chr$(0) anhÑngen
  *Umgebung_str.%="PATH="+Chr$(0)+Pfad_ag_auto$+Chr$(0)+Chr$(0)
  *Pfad_ag_auto_str.%=Pfad_ag_auto$
  *Datei_ag_autex.inf_str.%="AG_AUTEX.INF"
  *Ende_data_str.%=" ***"
Return
' ------------------------------------------------------------------------------
Procedure Zaehl_ag.inf(Anzahl_str.%)
  Local J_inf%
  If Exist(Datei_ag_autex.inf$)=True
    J_inf%=0
    Open "I",#1,Datei_ag_autex.inf$
    While Not Eof(#1)
      Line Input #1,Ag_autex.inf$
      If Instr(Ag_autex.inf$,Ende_data$)>0
        Inc J_inf%
      Else
        @Fehler_ag_autex.inf
      Endif
    Wend
    Close #1
    *Anzahl_str.%=J_inf%
  Else
    Out 2,7
    Files
    @Cconws("INFODATEI '"+Datei_ag_autex.inf$+"' NICHT VORHANDEN")
    Pause 100
    @Ende
  Endif
Return
' ------------------------------------------------------------------------------
Procedure Lade_ag.inf(Anzahl%)
  ' erase nicht nîtig lÑuft nur einmal
  Local N%
  Dim Ag_autex.inf$(Anzahl%-1)
  Open "I",#1,Datei_ag_autex.inf$
  For N%=0 To Dim?(Ag_autex.inf$())-1
    Line Input #1,Ag_autex.inf$(N%)
  Next N%
  Close #1
Return
' ------------------------------------------------------------------------------
Procedure Prg_aufruf
  Local N%
  For N%=0 To Dim?(Ag_autex.inf$())-1
    Len_kommentar%=Instr(Ag_autex.inf$(N%),Ende_data$)
    Datei$=Left$(Ag_autex.inf$(N%),Len_kommentar%-1)
    If Exist(Datei$)=True
      ' Immer wieder 'Chdir' man weis nicht was die andern verÑndern !
      Chdir Pfad_ag_auto$                        !ja kein chr$(0) anhÑngen
      @Cconws(Chr$(7)+" AusfÅhren: "+Datei$)
      Datei$=Datei$+Chr$(0)
      ' "GDOS" benîtigt den Path und die ASSIGN.SYS ist aufgerÑumt
      '                       Den andern kann's auch nicht schaden
      Exec 0,Datei$,"",Umgebung$ !ja nicht chr$(0)vergessen
    Else
      @Cconws(Chr$(7)+" DATEI '"+Datei$+"' NICHT VORHANDEN")
    Endif
  Next N%
Return
' ------------------------------------------------------------------------------
Procedure Fehler_ag_autex.inf
  @Cconws(Chr$(7)+" leider ist die Infodatei nicht vollstÑndig")
  @Cconws("")
  @Cconws(" die Zeile muû mit Leerzeichen und *** abgeschlossen sein: ' ***'")
  Pause 100
  @Ende
Return
' ------------------------------------------------------------------------------
Procedure Print_info
  Restore Info
  Do
    Read Info$
    Exit If Info$=Ende_data$
    @Cconws(Info$)
  Loop
Return
' ------------------------------------------------------------------------------
Procedure Cconws(String$)
  String$=String$+Chr$(10)+Chr$(13)+Chr$(0)
  Void Gemdos(9,L:Varptr(String$))
Return
' ------------------------------------------------------------------------------
Procedure Setz_mittel_aufl.
  Log_bleibt_gleich!=True
  Phy_bleibt_gleich!=True
  Res_1%=1
  Fehler=Xbios(5,L:Log_bleibt_gleich!,L:Phy_bleibt_gleich!,Res_1%)
Return
' ------------------------------------------------------------------------------
Procedure 4_farb_invers(Flg.invers_str.%)
  Void Xbios(7,0,&X111111000000000000000000)
  Void Xbios(7,1,&X111111000000000000000000)
  Void Xbios(7,2,&X111111000000000000000000)
  Void Xbios(7,3,&X111111000000011101110111)
  *Flg.invers_str.%=True
Return
' ------------------------------------------------------------------------------
Procedure 4_farb_normal(Flg.invers_str.%)
  Void Xbios(7,3,&X111111000000000000000000)
  Void Xbios(7,2,&X111111000000000000000000)
  Void Xbios(7,1,&X111111000000000000000000)
  Void Xbios(7,0,&X111111000000011101110111)
  *Flg.invers_str.%=False
Return
' ------------------------------------------------------------------------------
Procedure Ende
  ' Da sich die Anmeldung von AES UND VDI nicht verhindern lÑsst muû man hier
  ' wenigstens die Abmeldung verhindern. wichtig bei GDOS sonst Bomben.
  Clr.w__sp$=Mki$(&H4267)
  Trap_2$=Mki$(&H4E41)
  Pterm_0$=Clr.w__sp$+Trap_2$
  Adr.%=Varptr(Pterm_0$)
  Void C:Adr.%()
Return
' ------------------------------------------------------------------------------
